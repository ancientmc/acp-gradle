buildscript {
    repositories {
        mavenCentral()
        maven { url = 'https://github.com/ancientmc/ancientmc-maven/raw/maven' }
    }
    dependencies {
        classpath 'io.github.moist-mason:license-importer:1.0'
    }
}

plugins {
    id 'java-library'
    id 'java-gradle-plugin'
    id 'maven-publish'
}

group 'com.ancientmc.acp'
version '0.1.1-beta' + new Date().format('yyyyMMdd.HHmmss')

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

repositories {
    mavenCentral()
    mavenLocal()
    maven { url = 'https://github.com/ancientmc/ancientmc-maven/raw/maven' }
}

apply plugin: 'io.github.moist-mason.license-importer'

licenseimporter {
    url = 'https://raw.githubusercontent.com/ancientmc/license/master/LICENSE'
    output = file(project.projectDir.path + "\\LICENSE")
}

sourceSets {
    acp
}

configurations {
    acpImplementation
    implementation.extendsFrom acpImplementation
}

dependencies {
    acpImplementation gradleApi()
    acpImplementation 'commons-io:commons-io:2.11.0'
    acpImplementation 'com.google.code.gson:gson:2.10.1'
}

gradlePlugin {
    plugins {
        acp {
            id = 'com.ancientmc.acp'
            displayName = 'acp-plugin'
            description = 'Gradle plugin for Ancient Coder Pack (ACP) processes.'
            implementationClass = 'com.ancientmc.acp.ACPPlugin'
        }
    }
}

jar {
    from sourceSets.acp.output
    manifest {
        attributes(
                'Implementation-Title': 'acp-gradle',
                'Implementation-Version': version,
                'Main-Class': 'com.ancientmc.acp.ACPPlugin'
        )
    }
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            groupId = 'com.ancientmc.acp'
            artifactId = 'acp-gradle'
            version = '0.1.1-beta'
            pom {
                name = 'acp-gradle'
                description = 'Gradle plugin for Ancient Coder Pack (ACP) processes.'
                url = 'https://github.com/ancientmc/acp-gradle'
                licenses {
                    license {
                        name = 'AncientMC License'
                        url = 'https://github.com/ancientmc/license/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'mason'
                        name = 'Mason'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = 'ancientmc-maven'
            url = 'D:\\Programs\\Projects\\AncientMC\\ancientmc-maven\\'
        }
    }
}