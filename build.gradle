plugins {
    id 'java-library'
    id 'java-gradle-plugin'
    id 'maven-publish'
}

group 'com.ancientmc.acp'
version '0.2.0-beta-dev'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

repositories {
    mavenCentral()
    mavenLocal()
    maven { url = 'https://maven.minecraftforge.net/'}
}

sourceSets {
    acp
    modtools
}

configurations {
    acpImplementation
    modtoolsImplementation.extendsFrom acpImplementation
    implementation.extendsFrom acpImplementation, modtoolsImplementation
}

dependencies {
    acpImplementation gradleApi()
    acpImplementation 'commons-io:commons-io:2.11.0'
    acpImplementation 'com.google.code.gson:gson:2.10.1'
    acpImplementation 'net.minecraftforge:srgutils:0.4.13'

    modtoolsImplementation sourceSets.acp.output

    testImplementation gradleTestKit()
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
}

gradlePlugin {
    plugins {
        acp {
            id = 'com.ancientmc.acp'
            displayName = 'acp-plugin'
            description = 'Gradle plugin for Ancient Coder Pack (ACP) processes.'
            implementationClass = 'com.ancientmc.acp.ACPPlugin'
        }
        modtools {
            id = 'com.ancientmc.modtools'
            displayName = 'modtools'
            description = 'Gradle plugin containing useful modding tools for users working with ACP.'
            implementationClass = 'com.ancientmc.modtools.ModToolsPlugin'
        }
    }
}

jar {
    from sourceSets.acp.output
    from sourceSets.modtools.output
    manifest {
        attributes(
                'Implementation-Title': 'acp-gradle',
                'Implementation-Version': version,
        )
    }
}

task testJar(type: Copy, dependsOn: jar) {
    from jar
    into ('acp_test\\plugins\\')
    rename { 'acp.jar' }
}

test {
    dependsOn(':testJar')
    useJUnitPlatform()
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            groupId = project.group
            artifactId = 'acp-gradle'
            version = project.version
            pom {
                name = 'acp-gradle'
                description = 'Gradle plugin for Ancient Coder Pack (ACP) processes.'
                url = 'https://github.com/ancientmc/acp-gradle'
                licenses {
                    license {
                        name = 'AncientMC License'
                        url = 'https://github.com/ancientmc/license/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'moist-mason'
                        name = 'Mason'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = 'ancientmc-maven'
            url = 'D:\\Programs\\Projects\\AncientMC\\ancientmc-maven\\'
        }
    }
}