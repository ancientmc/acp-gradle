plugins {
    id 'java-library'
    id 'java-gradle-plugin'
    id 'maven-publish'
}

group 'com.ancientmc.acp'
version '0.1.0-beta'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

tasks.named('wrapper', Wrapper).configure {
    gradleVersion = '7.4'
    distributionType = Wrapper.DistributionType.ALL
}

repositories {
    mavenCentral()
    mavenLocal()
}

sourceSets {
    acp
}

configurations {
    acpImplementation.extendsFrom implementation
    retroguard
    mcinjector
    enigma
    quiltflower
    diffpatch
}

dependencies {
    acpImplementation gradleApi()
    acpImplementation 'commons-io:commons-io:2.8.0'
    acpImplementation 'com.google.code.gson:gson:2.9.1'
}

gradlePlugin {
    plugins {
        acp {
            id = "com.ancientmc.acp"
            displayName = "ACP-Gradle"
            description = "Gradle plugin for Ancient Coder Pack processes."
            implementationClass = "com.ancientmc.acp.ACPPlugin"
        }
    }
    automatedPublishing = true
}

jar {
    from sourceSets.acp.output
    manifest {
        attributes(
                'Implementation-Title': 'acp-gradle',
                'Implementation-Version': version,
                'Main-Class': 'com.ancientmc.acp.ACPPlugin'
        )
    }
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            groupId = 'com.ancientmc.acp'
            artifactId = 'ACP-Gradle'
            version = '0.1.0-beta'
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'ACP-Gradle'
                description = 'Gradle plugin for Ancient Coder Pack (ACP) processes.'
                url = 'https://github.com/moist-mason/acp-gradle'
                licenses {
                    license {
                        name = 'The GNU General Public License, v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }
                developers {
                    developer {
                        id = 'mason'
                        name = 'Mason'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = 'ancientmc-maven'
            url = 'D:\\Programs\\Projects\\AncientMC\\ancientmc-maven'
        }
    }
}