plugins {
    id 'java-library'
    id 'java-gradle-plugin'
    id 'maven-publish'
}

group 'com.entropy.rcp'
version '0.1.0-beta'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

tasks.named('wrapper', Wrapper).configure {
    gradleVersion = '7.4'
    distributionType = Wrapper.DistributionType.ALL
}

repositories {
    mavenCentral()
    mavenLocal()
}

sourceSets {
    rcp
}

configurations {
    rcpImplementation.extendsFrom implementation
    retroguard
    mcinjector
    enigma
    quiltflower
    diffpatch
}

dependencies {
    rcpImplementation gradleApi()
    rcpImplementation 'commons-io:commons-io:2.8.0'
    rcpImplementation 'com.google.code.gson:gson:2.9.1'
}

gradlePlugin {
    plugins {
        rcp {
            id = "com.entropy.rcp"
            displayName = "RCP-Gradle"
            description = "Gradle plugin for Retro Coder Pack processes."
            implementationClass = "com.entropy.rcp.RCPPlugin"
        }
    }
    automatedPublishing = true
}

jar {
    from sourceSets.rcp.output
    manifest {
        attributes(
                'Implementation-Title': 'rcp-gradle',
                'Implementation-Version': version,
                'Main-Class': 'com.entropy.rcp.RCPPlugin'
        )
    }
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            groupId = 'com.entropy.rcp'
            artifactId = 'RCP-Gradle'
            version = '0.1.0-beta'
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'RCP-Gradle'
                description = 'Gradle plugin for Retro Coder Pack (RCP) processes.'
                url = 'https://github.com/moist-mason/rcp-gradle'
                licenses {
                    license {
                        name = 'The GNU General Public License, v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }
                developers {
                    developer {
                        id = 'mason'
                        name = 'Mason'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = 'entropy-maven'
            url = 'https://github.com/entropymc/entropy-maven/raw/maven'
        }
    }
}