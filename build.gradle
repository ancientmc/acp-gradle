plugins {
    id 'java-library'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '1.0.0'
}

group 'com.entropy.rcp'
version '0.1.0-beta'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

tasks.named('wrapper', Wrapper).configure {
    gradleVersion = '7.4'
    distributionType = Wrapper.DistributionType.ALL
}

repositories {
    mavenCentral()
    mavenLocal()
}

sourceSets {
    rcp
}

configurations {
    rcpImplementation.extendsFrom implementation
    retroguard
    mcinjector
    enigma
    quiltflower
    diffpatch
}

dependencies {
    rcpImplementation gradleApi()
    rcpImplementation 'commons-io:commons-io:2.8.0'
    rcpImplementation 'com.google.code.gson:gson:2.9.1'
}

gradlePlugin {
    plugins {
        rcp {
            id = "com.entropy.rcp"
            displayName = "RCP-Gradle"
            description = "Gradle plugin for Retro Coder Pack processes."
            implementationClass = "com.entropy.rcp.RCPPlugin"
        }
    }
    automatedPublishing = true
}

pluginBundle {
    website = "https://github.com/moist-mason/rcp-gradle"
    vcsUrl = "https://github.com/moist-mason/rcp-gradle.git"
    tags = ['minecraft', 'mcp', 'rcp']
}

jar {
    from sourceSets.rcp.output
    manifest {
        attributes(
                'Implementation-Title': 'rcp-gradle',
                'Implementation-Version': version,
                'Main-Class': 'com.entropy.rcp.RCPPlugin'
        )
    }
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            groupId = 'com.entropy.rcp'
            artifactId = 'rcp-gradle'
            version = '0.1.0-beta'
        }
    }
    repositories {
        maven {
            name = 'entropyMaven'
            url = layout.buildDirectory.dir('maven')
        }
    }
}